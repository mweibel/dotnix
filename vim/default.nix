# Vim, with a set of extra packages (extraPackages) and a custom vimrc
# (./vimrc). The final vimrc file is generated by vimUtils.vimrcFile and
# bundles all the packages with the custom vimrc.
{ symlinkJoin
, makeWrapper
, vim_configurable
, vimUtils
, vimPlugins
, lib
, stdenv
}:
let
  vim = if stdenv.isDarwin then vim_configurable.overrideAttrs (
    oa:
      {
        configureFlags = lib.filter
          (f: ! lib.hasPrefix "--enable-gui" f) oa.configureFlags;
      }
  ) else vim_configurable;

  extraPackages = with vimPlugins;
    [
      # currently no extra packages
    ];
  customRC = vimUtils.vimrcFile
    {
      customRC = builtins.readFile ./vimrc;
      packages.mvc.start = extraPackages;
    };
in
symlinkJoin {
  name = "vim";
  buildInputs = [ makeWrapper ];
  postBuild = ''
    wrapProgram "$out/bin/vim" \
    --add-flags "-u ${customRC}"
  '';
  paths = [ vim ];
}
